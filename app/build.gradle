apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

apply from: '../quality/checkstyle.gradle'
apply from: '../quality/findbugs.gradle'
apply from: '../quality/pmd.gradle'
apply from: '../quality/jacoco.gradle'

def isOnCi() {
    def ci = System.getenv("CI")
    return (ci != null && ci == "true")
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "me.maxdev.popularmoviesapp"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def STRING = "String"
    def UNDEFINED = '"UNDEFINED"'
    def THE_MOVIE_DB_API_KEY = "THE_MOVIE_DB_API_KEY"

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-glide.pro',
                    'proguard-retrofit.pro',
                    'proguard-rxjava.pro'
        }
    }
    buildTypes.each {
        if (!isOnCi()) {
            it.buildConfigField STRING, THE_MOVIE_DB_API_KEY, "\"53470b56a60668274e1dd9f84d882564\""
        } else {
            it.buildConfigField STRING, THE_MOVIE_DB_API_KEY, UNDEFINED
        }
    }

    lintOptions {
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK
        disable 'GoogleAppIndexingWarning' // App does not have deep linking
        disable 'IconDipSize'
        warningsAsErrors true
        abortOnError true
        textReport true
        textOutput 'stdout'
    }

    // Log unit tests.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
        jacoco {
            includeNoLocationClasses = true
        }
    }

    // Log instrumentation tests.
    tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
        task.doFirst {
            logging.level = LogLevel.INFO
        }
        task.doLast {
            logging.level = LogLevel.LIFECYCLE
        }
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {

    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    if (keystoreProperties != null &&
            keystoreProperties.containsKey('storeFile') &&
            keystoreProperties.containsKey('storePassword') &&
            keystoreProperties.containsKey('keyAlias') &&
            keystoreProperties.containsKey('keyPassword')) {

        println 'keystore.properties found. Sign APK'
        android.signingConfigs.config.storeFile file(keystoreProperties['storeFile'])
        android.signingConfigs.config.storePassword keystoreProperties['storePassword']
        android.signingConfigs.config.keyAlias keystoreProperties['keyAlias']
        android.signingConfigs.config.keyPassword keystoreProperties['keyPassword']

    } else {
        println 'keystore.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'keystore.properties not found'
    android.buildTypes.release.signingConfig = null
}

ext {
    supportLibraryVersion = "24.1.1"
    testingSupportLibraryVersion = "0.5"
    okHttpVersion = "3.4.1"
    retrofitVersion = "2.1.0"
    butterknifeVersion = "8.1.0"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:support-v4:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.android.support:support-annotations:${supportLibraryVersion}"

    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"

    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.trello:rxlifecycle:0.6.1'
    compile 'com.trello:rxlifecycle-components:0.6.1'

    compile "com.jakewharton:butterknife:${butterknifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterknifeVersion}"

    compile 'com.github.bumptech.glide:glide:3.7.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    androidTestCompile "com.android.support.test:runner:${testingSupportLibraryVersion}"
    androidTestCompile "com.android.support.test:rules:${testingSupportLibraryVersion}"
}
